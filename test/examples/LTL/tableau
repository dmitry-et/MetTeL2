//equality rules 
@l{l2} / @l2{l} priority 1$;
@l~{l2} / @l2{l2} priority 1$;

@l P / @l{l} priority 1 $;

@l P  @l{l2} /  @l2 P  priority 2$;

//Decomposition rules 
@l ~(~P) / @l P  priority 1$; 

@l(P|Q) / @l P $| @l Q  priority 3$;
@l~(P|Q) / @l~P @l~Q  priority 1$;

@l(P&Q) / @l P @l Q priority 1$;
@l ~(P&Q) / @l ~P $| @l ~Q priority 3$;

@l(()P) / @f(l)P  priority 7$;
@l ~(()P) / @l ()(~P) priority 1$;
//@l ~(()P) @f(l){f(l)} / @f(l)~P  priority 2$;

//Diamond
@l (<>P) / @l (E(P,l)) priority 1 $;
@l (E(P,l1)) / @l P ((E(P,l1)) <-> (<>P)) $| @f(l) (E(P,l1)) priority 7$;

@l (~(<>P)) / @l ([](~P)) priority 1$;

//Box
@l []P / @l P @f(l)[]P priority 7$;
@l ~([]P) / @l <>(~P) priority 1$;

//Until
@l(C U D) / @l (EU(C,D,l)) priority 1$;
@l (EU(C,D,l1)) / @l D ((EU(C,D,l1))<->(C U D)) $| @l C @f(l) (EU(C,D,l1)) priority 7$;
@l~(C U D) / (~D) W ((~C) & (~D)) priority 1$;

//While
@l(C W D) / @l (EW(C,D,l)) priority 1$;
@l (EW(C,D,l1)) / @l D ((EW(C,D,l1))<->(C W D)) $| @l C @f(l) (EW(C,D,l1)) priority 7$;

//@l~(C U D)/ @l (~D U ~(C | D)) $| @l ~(~false U ~C) priority 4$;

// Closure rule
@l P  @l (~P) / priority 0$;
@l false / priority 0$;
@l ~(true)/ priority 0$;

//Blocking related
@l{l0} / [l=l0]  priority 1$;
[l=l0] / @l{l0}  priority 1$;
@l~{l0} / ~([l=l0])  priority 1$;
~([l=l0]) / @l~{l0}  priority 1$;
@l{l} @l0{l0} / [l=l0] $| ~([l=l0])  priority 6$;

@l (EU(C,D,l)) /  priority 8$;
@l (EW(C,D,l)) /  priority 8$;
@l (E(C,l)) /  priority 8$;
