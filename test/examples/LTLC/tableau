//equality rules
@l{l2} / @l2{l} priority 1$;
@l~{l2} / @l2{l2} priority 1$;

@l P / @l{l} priority 1 $;

@l P  @l{l2} /  @l2 P  priority 2$;

//Decomposition rules
@l ~(~P) / @l P  priority 1$;

@l~P @l(P|Q) / @l Q priority 2$;
@l P @l(~P|Q) / @l Q priority 2$;
@l~Q @l(P|Q) / @l P priority 2$;
@l Q @l(P|~Q) / @l P priority 2$;

@l P @l~(P&Q) / @l~Q priority 2$;
@l~P @l~(~P&Q) / @l~Q priority 2$;
@l Q @l~(P&Q) / @l~P priority 2$;
@l~Q @l~(P&~Q) / @l~P priority 2$;

@l P @l(P->Q) / @l Q priority 2$;
@l~Q @l(P -> Q) / @l~P priority 2$;
@l Q @l(P -> ~Q) / @l~P priority 2$;

@l(P|Q) / @l P $| @l Q  priority 3$;
@l~(P|Q) / @l~P @l~Q  priority 1$;

@l(P&Q) / @l P @l Q priority 1$;
@l ~(P&Q) / @l ~P $| @l ~Q priority 3$;

@l(P->Q) / @l ~P $| @l Q priority 3$;
@l~(P->Q) / @l P @l ~Q priority 1$;

//@l(P->Q) / @l(~P|Q) priority 1$;
//@l~(P->Q) / @l(P&~Q) priority 1$;

@l(P<->Q) / @l (P->Q) @l (Q->P) priority 1$;
@l~(P<->Q) / @l ~(P->Q) $| @l ~(Q->P) priority 3$;

@l(()P) / @f(l)P  priority 7$;
@l ~(()P) / @l ()(~P) priority 1$;
//@l ~(()P) @f(l){f(l)} / @f(l)~P  priority 2$;

//Diamond
@l (<>P) / @l (E(P)) priority 1 $;
@l (E(P)) / @l P ((E(P)) <-> (<>P)) $| @f(l) (E(P)) priority 7$;

@l (~(<>P)) / @l ([](~P)) priority 1$;

//Box
@l []P / @l P @f(l)[]P priority 7$;
@l ~([]P) / @l <>(~P) priority 1$;

//Until
@l(C U D) / @l (EU(C,D)) priority 1$;
@l (EU(C,D)) / @l D ((EU(C,D))<->(C U D)) $| @l C @f(l) (EU(C,D)) priority 7$;
@l~(C U D) / @l((~D) W ((~C) & (~D))) priority 1$;

//Wait for
//@l(C W D) / @l (EW(C,D,l)) priority 1$;
@l(C W D) / @l D $| @l C @f(l) (C W D) priority 7$;

//@l~(C U D)/ @l (~D U ~(C | D)) $| @l ~(~false U ~C) priority 4$;

// Closure rule
@l P  @l (~P) / priority 0$;
@l false / priority 0$;
@l ~(true)/ priority 0$;

//Blocking related
@l{l0} / [l=l0]  priority 1$;
[l=l0] / @l{l0}  priority 1$;
@l~{l0} / ~([l=l0])  priority 1$;
~([l=l0]) / @l~{l0}  priority 1$;
@l{l} @l0{l0} / [l=l0] $| ~([l=l0])  priority 6$;

@l (EU(C,D)) / ((EU(C,D))<->(badLoop)) priority 8$;
//@l (EW(C,D,l)) /  priority 8$;
@l (E(C)) / ((E(C))<->(badLoop))  priority 8$;

@l (badLoop) / priority 8$;


//Constraints

@l (C02(p,q)) / @l (C02(q,p)) priority 1$;
@l (C12(p,q)) / @l (C12(q,p)) priority 1$;
@l (C22(p,q)) / @l (C22(q,p)) priority 1$;

@l (C03(p,q,r)) / @l (C03(q,r,p)) priority 1$;
@l (C13(p,q,r)) / @l (C13(q,r,p)) priority 1$;
@l (C23(p,q,r)) / @l (C23(q,r,p)) priority 1$;
@l (C33(p,q,r)) / @l (C33(q,r,p)) priority 1$;
@l (C03(p,q,r)) / @l (C03(q,p,r)) priority 1$;
@l (C13(p,q,r)) / @l (C13(q,p,r)) priority 1$;
@l (C23(p,q,r)) / @l (C23(q,p,r)) priority 1$;
@l (C33(p,q,r)) / @l (C33(q,p,r)) priority 1$;


@l (C02(p,q)) @l p / priority 0 $;
@l (C02(p,q)) @l ~p / @l ~q priority 2 $;

@l (C12(p,q)) @l p / @l ~q priority 2 $;
@l (C12(p,q)) @l ~p / @l q priority 2 $;

@l (C22(p,q)) @l p / @l q priority 2 $;
@l (C22(p,q)) @l ~p / priority 0 $;

@l (C03(p,q,r)) @l p / priority 0 $;
@l (C03(p,q,r)) @l ~p / @l (C02(q,r)) priority 2 $;

@l (C13(p,q,r)) @l p / @l (C02(q,r)) priority 2 $;
@l (C13(p,q,r)) @l ~p / @l (C12(q,r)) priority 2 $;

@l (C23(p,q,r)) @l p / @l (C12(q,r)) priority 2 $;
@l (C23(p,q,r)) @l ~p / @l (C22(q,r)) priority 2 $;

@l (C33(p,q,r)) @l p / @l (C22(q,r)) priority 2 $;
@l (C33(p,q,r)) @l ~p / priority 0 $;

@l (C02(p,p)) / @l ~p priority 1$;
@l (C12(p,p)) / @l p priority 1$;
@l (C22(p,p)) / priority 0$;

@l (C03(p,p,q)) / @l (C02(p,q)) priority 1$;
@l (C13(p,p,q)) / @l (C12(p,q)) priority 1$;
@l (C23(p,p,q)) / @l (C22(p,q)) priority 1$;
@l (C33(p,p,q)) / priority 0$;
